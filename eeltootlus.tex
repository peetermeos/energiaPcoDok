\subsection{Kaevandused ja primaarenergia}
Lihtsustuse mõttes ei küsi mudel sisendis kaevanduse ja karjääri igale tootele portfellis vaid arvutab kütteväärtuse järgi hinnad ise ühe etteantud toote ja selle kütteväärtuse järgi.

Muutuvkulud primaarenergia allikatele, kus ei ole rikastusvarikut on kirjeldatud järgnevalt.
\begin{verbatim}
fs_vc(k, feedstock, year)$(not k_enrichment(k)
                                and not sameas(k, "Hange")
                                and fs_k(k, feedstock)
                               )
  = sum(p2$(k_mines(k, p2)
    and enrichment_coef(p2, k, feedstock) > 0), fs_vc(k, p2, year) 
       / enrichment_coef(p2, k, feedstock));
\end{verbatim}

Rikastusvabrikuga primaarenergia allikatele on kaeve muutuvkulude arvutamine tehtud nii.
\begin{verbatim}
fs_vc(k, feedstock, year)$(k_enrichment(k)
      and fs_k(k, feedstock)
      and fs_vc(k, feedstock, year) = 0
      and(cv("Energeetiline", k, "MWh") - cv("Aheraine", k, "MWh")) > 0)
  =
  fs_vc(k, "Energeetiline", year)
   * (1
      -
     (cv("Energeetiline", k, "MWh") - cv(feedstock, k, "MWh"))
      /
     (cv("Energeetiline", k, "MWh") - cv("Aheraine", k, "MWh"))
     );
\end{verbatim}

\subsection{Sekundaarenergia}
Elektri hinnad tuleb tunnipõhisest tabelist ümber arvutada sloti keskmisteks hindadeks. Kuigi lõplikult formulatsioonis vahet ei oleks, teeb selle eeltöötluses arvutamine mudeli kokkupanemist eriti stohhastilises variantides oluliselt kiiremaks.
\begin{verbatim}
el_price_slot(time_t, slot) =
sum((year, month, day, weekday, time_hour)
      $(y_m_t
    and days
    and gdow(jdate(%year_1% + ord(year) - 1, ord(month), ord(day))) = ord(weekday)
    and slot_hours(slot, weekday, time_hour)
      ),  elektri_referents(year, month, day, time_hour)
)
/
sum((year, month, day, weekday, time_hour)
      $(y_m_t
    and days
    and gdow(jdate(%year_1% + ord(year) - 1, ord(month), ord(day))) = ord(weekday)
    and slot_hours(slot, weekday, time_hour)
    and elektri_referents(year, month, day, time_hour) > 0
       ),  1
)
;
\end{verbatim}

\subsection{Tootmine}
Kasuteguritest tuleb arvutada sissemineva primaarenergia ja väljuva elektri paarid. 

\begin{verbatim}
efficiency(t_el, para_lk, "b")$(efficiency(t_el, para_lk, "b") > 0) = efficiency(t_el, para_lk, "b") + efficiency_shift;

* Correct efficiencies according to minimum loads
* efficiency(t_el, "3", "a") = min_koormus_el(t_el);
efficiency(t_el, "2", "a") = efficiency(t_el, "3", "a") - 0.1;

eff_lookup(t_el, para_lk, "a")$(efficiency(t_el, para_lk, "b") > 0) = efficiency(t_el, para_lk, "a");
eff_lookup(t_el, para_lk, "b")
                        $(efficiency(t_el, para_lk, "b") > 0
                      and efficiency(t_el, para_lk, "a") > 0)
      = efficiency(t_el, para_lk, "a") / efficiency(t_el, para_lk, "b");

eff_lookup(t_el, para_lk, "b")
                        $(efficiency(t_el, para_lk, "b") = 0
                      and efficiency(t_el, para_lk, "a") > 0)
      = eff_lookup(t_el, para_lk+1, "b")-1;

ht_efficiency(t_el)$(max_load_ht(t_el) = 0) = 1;

t_rg_total(year, month) = sum(t_el, t_rg(t_el, year, month));
failure_s(sim, t, year)  = failure_rate(t, year);

* Convert MJ/kg to MWh/t
cv_min(t, year, month) = cv_min(t, year, month) / 3.6;
\end{verbatim}

Remondigraafikud tuleb ümber arvutada päevapõhisele vormile (sisendid on algus- ja lõpukuupäev).
\begin{verbatim}
$macro mr_day(t, r_num, type)   trunc(t_maintenance(t, r_num, type) / 1E4)
$macro mr_month(t, r_num, type) trunc((t_maintenance(t, r_num, type) - (mr_day(t, r_num, type)  * 1E4)) / 1E2)
$macro mr_year(t, r_num, type)  (2000 + t_maintenance(t, r_num, type) - (mr_day(t, r_num, type) * 1E4) - (mr_month(t, r_num, type) * 1E2))

r_beg_time(t, r_num)     $(t_maintenance(t, r_num, 'algus') > 0) = jdate(mr_year(t, r_num, 'algus'), mr_month(t, r_num, 'algus'), mr_day(t, r_num, 'algus')) - jdate(%year_1%, 1, 1) + 1;
r_end_time("1", t, r_num)$(t_maintenance(t, r_num, 'lopp')  > 0) = jdate(mr_year(t, r_num, 'lopp'),  mr_month(t, r_num, 'lopp'), mr_day(t, r_num, 'lopp')) - jdate(%year_1%, 1, 1) + 1;

* Calculate day numbers for maintenance days
t_mx_schedule(time_t, slot, t) = sum((r_num, cal_time)$(
                                            (ord(cal_time) ge r_beg_time(t, r_num))
                                        and (ord(cal_time) le r_end_time("1", t, r_num))
                                        and cal_t(time_t, cal_time)),
                                          sum((weekday, time_hour)$(
                                              slot_hours(slot, weekday, time_hour)
                                          and wkday_number_cal(cal_time) = ord(weekday)
                                                    ), 1));
\end{verbatim}

Slotis sisalduvad töötunnid tuleb ümber arvutada vastavalt avariilisusele ja remondigraafikutele.
\begin{verbatim}
* Calculate correct number of hours in slot adjusted by maintenance days
slot_length_s(sim, time_t, slot, t) = slot_length_orig(time_t, slot, t) - t_mx_schedule(time_t, slot, t);
slot_length_s(sim, time_t, slot, t) = slot_length_s(sim, time_t, slot, t) * (1 -  sum((year, month)$y_m_t, failure_rate(t, year)));
slot_length_s(sim, time_t, slot, t)$(slot_length_s(sim, time_t, slot, t) < 0) = 0;

*slot_length_s(sim, time_t, slot, t_el)$(sum((year, month)$y_m_t, max_load_pu(t_el, year, month)) = 0) = 0;
slot_length(time_t, slot, t_el) = sum(sim$(ord(sim) = 1), slot_length_s(sim, time_t, slot, t_el));
\end{verbatim}

Õli tootmise maksimaalne võimekus päevas tuleb ümber arvutada primaarenergiast sekundaarenergiasse ning võtta arvesse remondi- ja puhastuspäevi.

\begin{verbatim}
max_load_ol_s(sim, t_ol, year, month)$(sum((time_t)$y_m_t, 1) > 0) =
* Tons of oilshale in day
   max_load_ol_temp(t_ol, year, month)
   * sum((time_t, slot)$y_m_t, slot_length_orig(time_t, slot, t_ol))
   / sum((time_t)$y_m_t, 1)
* Tons of oil in day
   * yield_oil(t_ol, year)
* Correct with days of maintenace and cleanings
   * (days_in_month_l(year, month)
      - p_days_month_oil(t_ol, year, month)
      - r_days_month_oil(t_ol, year, month)
      )
   / days_in_month_l(year, month)
;
\end{verbatim}

Elektri tootmisvõimekust tuleb alla kalibreerida vastavalt etteantud võimsus\-kadude tabelile. ENEFITi elektritootmine peab arvesse võtma õlitehase remondi- ja pu\-hastus\-päevi.
\begin{verbatim}
max_load_pu(t_el, year, month)$(max_load_pu(t_el, year, month) >  misc_lost_pwr(t_el, year ,month))
  = max_load_pu(t_el, year, month) - misc_lost_pwr(t_el, year, month);

max_load_el_s(sim, t_el, year, month)$(max_load_el_s(sim, t_el, year, month) >  misc_lost_pwr(t_el, year ,month))
  = max_load_el_s(sim, t_el, year, month) - misc_lost_pwr(t_el, year ,month);

* Correction for oil plant elecritity production
max_load_el_s(sim, t_el, year, month)$t_ol(t_el) = max_load_el_s(sim, t_el, year, month)
  * (days_in_month_l(year, month)
        - sum(t_ol$sameas(t_ol, t_el), p_days_month_oil(t_ol, year, month) + r_days_month_oil(t_ol, year, month))
        )
     / days_in_month_l(year, month);
\end{verbatim}

Elektri minimaalsed tootmiskogused tuleb peak- ja offpeak koguste sisendist ümber arvutada slottide minimaalseteks kogusteks vastavalt kalendri resolutsioonile.

\begin{verbatim}
min_production(time_t, slot) =
  sum((year, month, cal_time_sub, weekday, time_hour)$(y_m_t
                             and cal_t(time_t, cal_time_sub)
                             and wkday_number_cal_sub(cal_time_sub) = ord(weekday)
                             and slot_hours(slot, weekday, time_hour)
                             and (   ord(time_hour) < 7
                                  or ord(time_hour) > 20
                                  or day_type(time_t) > 0)
                              ),
         t_el_min_sum_offpeak(year, month))
  +
  sum((year, month, cal_time_sub, weekday, time_hour)$(y_m_t
                             and cal_t(time_t, cal_time_sub)
                             and wkday_number_cal_sub(cal_time_sub) = ord(weekday)
                             and slot_hours(slot, weekday, time_hour)
                             and ord(time_hour) > 6
                             and ord(time_hour) < 21
                             and day_type(time_t) = 0
                             ),
       t_el_min_sum_peak(year, month))
;
\end{verbatim}